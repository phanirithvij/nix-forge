module ConfigDecoder exposing (App, Config, OptionsFilter, Package, configDecoder, packageDecoder)

import Dict
import Json.Decode as Decode


type alias OptionsFilter =
    Dict.Dict String (List String)


type alias Config =
    { repositoryUrl : String
    , apps : List App
    , packages : List Package
    , packagesFilter : OptionsFilter
    , appsFilter : OptionsFilter
    }


type alias App =
    { name : String
    , description : String
    , version : String
    , usage: String
    , vm : AppVm
    }


type alias AppVm =
    { enable : Bool
    }


type alias Package =
    { name : String
    , description : String
    , version : String
    , homePage : String
    , mainProgram : String
    , builder : String
    }


optionsFilterDecoder : Decode.Decoder OptionsFilter
optionsFilterDecoder =
    Decode.dict (Decode.list Decode.string)


configDecoder : Decode.Decoder Config
configDecoder =
    Decode.map5 Config
        (Decode.field "repositoryUrl" Decode.string)
        (Decode.field "apps" (Decode.list appDecoder))
        (Decode.field "packages" (Decode.list packageDecoder))
        (Decode.field "packagesFilter" optionsFilterDecoder)
        (Decode.field "appsFilter" optionsFilterDecoder)


appDecoder : Decode.Decoder App
appDecoder =
    Decode.map5 App
        (Decode.field "name" Decode.string)
        (Decode.field "description" Decode.string)
        (Decode.field "version" Decode.string)
        (Decode.field "usage" Decode.string)
        (Decode.field "vm" appVmDecoder)


appVmDecoder : Decode.Decoder AppVm
appVmDecoder =
    Decode.map AppVm
        (Decode.field "enable" Decode.bool)


packageBuilder : Decode.Decoder String
packageBuilder =
    Decode.field "build" (Decode.dict (Decode.maybe (Decode.oneOf [ Decode.field "enable" Decode.bool, Decode.bool ])))
        |> Decode.map findEnabledBuilder


findEnabledBuilder : Dict.Dict String (Maybe Bool) -> String
findEnabledBuilder dict =
    dict
        |> Dict.filter (\_ value -> value == Just True)
        |> Dict.keys
        |> List.head
        |> Maybe.withDefault "none"


packageDecoder : Decode.Decoder Package
packageDecoder =
    Decode.map6 Package
        (Decode.field "name" Decode.string)
        (Decode.field "description" Decode.string)
        (Decode.field "version" Decode.string)
        (Decode.field "homePage" Decode.string)
        (Decode.field "mainProgram" Decode.string)
        packageBuilder
