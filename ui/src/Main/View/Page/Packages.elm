module Main.View.Page.Packages exposing (..)

import Html exposing (Html, a, code, div, h5, span, text)
import Html.Attributes exposing (attribute, class, href, id, rel, style, target, title)
import Main.Config exposing (..)
import Main.Config.Package exposing (..)
import Main.Helpers.AppUrl exposing (..)
import Main.Helpers.Html exposing (..)
import Main.Helpers.Markdown as Markdown
import Main.Helpers.Nix exposing (..)
import Main.Icons exposing (..)
import Main.Model exposing (..)
import Main.Model.Page exposing (..)
import Main.Model.Preferences exposing (..)
import Main.Route as Route exposing (..)
import Main.Update exposing (..)
import Main.View.Pagination exposing (..)


viewPagePackagesLink : Html Update
viewPagePackagesLink =
    let
        onClickRoute =
            Route_Packages defaultRoutePackages
    in
    a
        [ href (onClickRoute |> Route.toString)
        , style "color" "inherit"
        , style "text-decoration" "none"
        , style "cursor" "pointer"
        , class "nav-link px-0 fw-bold"
        , title "View available packages"
        , attribute "aria-label" "View available packages"
        , onClick (Update_Route onClickRoute)
        ]
        [ text "Packages" ]


viewPagePackages : Model -> PagePackages -> Html Update
viewPagePackages model pagePackages =
    viewPagination
        pagePackages.pagePackages_pagination
        (viewPagePackagesItem model pagePackages)
        (\modifyRoutePagination ->
            let
                routePackages =
                    pagePackages.pagePackages_route
            in
            Route_Packages
                { routePackages
                    | routePackages_pagination = routePackages.routePackages_pagination |> modifyRoutePagination
                    , routePackages_focus = Nothing
                }
        )


viewPagePackagesItem : Model -> PagePackages -> Package -> Html Update
viewPagePackagesItem model pagePackages package =
    let
        routePackages =
            pagePackages.pagePackages_route

        itemId =
            package.package_name

        onClickRoute =
            Route_Packages
                { routePackages
                    | routePackages_focus = Just <| RoutePackagesFocus_Package itemId
                }
    in
    a
        [ class "list-item list-group-item list-group-item-action flex-column align-items-start"
        , href (onClickRoute |> Route.toString)
        , id itemId
        , onClick (Update_Route onClickRoute)
        ]
        [ div [ class "d-flex w-100 justify-content-between" ]
            [ h5
                [ class "mb-1"
                ]
                [ code [] [ text package.package_name ]
                ]
            , span [ style "font-variant" "italic" ] [ text package.package_version ]
            ]
        , div []
            (package.package_description |> Markdown.render)
        , div []
            [ a
                [ href <| showPackageRecipeLink model package
                , target "_blank"
                , rel "noopener"
                ]
                [ text "Forge Recipe" ]
            ]
        ]


showPackageRecipeLink : Model -> Package -> String
showPackageRecipeLink model package =
    String.join "/"
        [ model.model_config.config_repository |> showNixUrl
        , "blob/" ++ commit
        , model.model_config.config_recipe.configRecipe_packages
        , package.package_name
        , "recipe.nix"
        ]
