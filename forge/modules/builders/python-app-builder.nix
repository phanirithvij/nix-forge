{
  inputs,
  config,
  lib,
  flake-parts-lib,
  ...
}:

let
  inherit (flake-parts-lib) mkPerSystemOption;
in
{
  options = {
    perSystem = mkPerSystemOption (
      {
        config,
        pkgs,
        sharedBuildAttrs,
        ...
      }:
      {
        options = {
          forge.packages = lib.mkOption {
            type = lib.types.listOf (
              lib.types.submodule {
                options = {
                  build.pythonAppBuilder = {
                    enable = lib.mkEnableOption ''
                      Python application builder for executable Python programs.

                      Uses buildPythonApplication which prevents the package from being used as a dependency'';
                    packages = {
                      build-system = lib.mkOption {
                        type = lib.types.listOf lib.types.package;
                        default = [ ];
                        description = "PEP-517 build system dependencies.";
                        example = lib.literalExpression "[ pkgs.python3Packages.setuptools pkgs.python3Packages.wheel ]";
                      };
                      build = lib.mkOption {
                        type = lib.types.listOf lib.types.package;
                        default = [ ];
                        description = ''
                          Native build-time dependencies.

                          Use this for tools needed during the build, such as pkg-config or compilers.
                        '';
                        example = lib.literalExpression "[ pkgs.pkg-config pkgs.kaitai-struct-compiler ]";
                      };
                      run = lib.mkOption {
                        type = lib.types.listOf lib.types.package;
                        default = [ ];
                        description = ''
                          Native runtime dependencies.

                          Use this for non-Python libraries or tools needed at runtime.
                        '';
                        example = lib.literalExpression "[ pkgs.openssl pkgs.sqlite ]";
                      };
                      dependencies = lib.mkOption {
                        type = lib.types.listOf lib.types.package;
                        default = [ ];
                        description = "Runtime dependencies (PEP-621).";
                        example = lib.literalExpression "[ pkgs.python3Packages.click pkgs.python3Packages.requests ]";
                      };
                      optional-dependencies = lib.mkOption {
                        type = lib.types.attrsOf (lib.types.listOf lib.types.package);
                        default = { };
                        description = ''
                          PEP-621 optional dependencies (extras).

                          These are additional dependencies that can be installed optionally.
                        '';
                        example = lib.literalExpression ''
                          {
                            dev = [ pkgs.python3Packages.pytest ];
                            redis = [ pkgs.python3Packages.redis ];
                          }
                        '';
                      };
                      check = lib.mkOption {
                        type = lib.types.listOf lib.types.package;
                        default = [ ];
                        description = ''
                          Test dependencies.

                          Packages needed to run the test suite. When non-empty, tests are
                          automatically enabled (doCheck = true).
                        '';
                        example = lib.literalExpression "[ pkgs.python3Packages.pytestCheckHook ]";
                      };
                    };
                    importsCheck = lib.mkOption {
                      type = lib.types.listOf lib.types.str;
                      default = [ ];
                      description = ''
                        List of Python modules to verify can be imported after installation.

                        This provides a simple smoke test to ensure the package was built correctly.
                      '';
                      example = [
                        "myapp"
                        "myapp.cli"
                      ];
                    };
                    relaxDeps = lib.mkOption {
                      type = lib.types.either lib.types.bool (lib.types.listOf lib.types.str);
                      default = [ ];
                      description = ''
                        Remove version constraints from specified dependencies.

                        Use when the package requires specific versions but works fine with versions in nixpkgs.
                        Set to true to relax all dependencies, or provide a list of dependency names.
                      '';
                      example = [
                        "click"
                        "attrs"
                      ];
                    };
                    disabledTests = lib.mkOption {
                      type = lib.types.listOf lib.types.str;
                      default = [ ];
                      description = ''
                        List of pytest test names to skip.

                        Useful for disabling flaky or network-dependent tests.
                      '';
                      example = [
                        "test_network"
                        "test_integration"
                      ];
                    };
                  };
                };
              }
            );
          };
        };

        config = {
          packages =
            let
              cfg = config.forge.packages;

              pythonAppBuilderPkgs = lib.listToAttrs (
                map (pkg: {
                  name = pkg.name;
                  value = pkgs.callPackage (
                    # Derivation start
                    { }:
                    pkgs.python3Packages.buildPythonApplication (
                      finalAttrs:
                      {
                        pname = pkg.name;
                        version = pkg.version;
                        format = "pyproject";
                        src = sharedBuildAttrs.pkgSource pkg;
                        patches = pkg.source.patches;
                        build-system = pkg.build.pythonAppBuilder.packages.build-system;
                        nativeBuildInputs = pkg.build.pythonAppBuilder.packages.build;
                        buildInputs = pkg.build.pythonAppBuilder.packages.run;
                        dependencies = pkg.build.pythonAppBuilder.packages.dependencies;
                        optional-dependencies = pkg.build.pythonAppBuilder.packages.optional-dependencies;
                        nativeCheckInputs = pkg.build.pythonAppBuilder.packages.check;
                        doCheck = pkg.build.pythonAppBuilder.packages.check != [ ];
                        pythonImportsCheck = pkg.build.pythonAppBuilder.importsCheck;
                        pythonRelaxDeps = pkg.build.pythonAppBuilder.relaxDeps;
                        disabledTests = pkg.build.pythonAppBuilder.disabledTests;
                        passthru = sharedBuildAttrs.pkgPassthru pkg finalAttrs.finalPackage;
                        meta = sharedBuildAttrs.pkgMeta pkg;
                      }
                      // pkg.build.extraAttrs
                      // lib.optionalAttrs pkg.build.debug sharedBuildAttrs.debugShellHookAttr
                    )
                    # Derivation end
                  ) { };
                }) (lib.filter (p: p.build.pythonAppBuilder.enable == true) cfg)
              );
            in
            pythonAppBuilderPkgs;
        };
      }
    );
  };
}
