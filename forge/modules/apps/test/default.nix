{
  lib,

  app,
  config,
  pkgs,
  ...
}:
{
  options = {
    requirements = lib.mkOption {
      type = lib.types.listOf lib.types.package;
      default = [ ];
      description = "Additional packages required for running tests.";
      example = lib.literalExpression "[ pkgs.curl pkgs.jq ]";
    };

    script = lib.mkOption {
      type = lib.types.str;
      default = ''
        echo "Test script"
      '';
      description = ''
        Bash script to run application tests inside a NixOS machine.

        The application's services are available in the machine.
        Run with: nix build .#<app>.test
      '';
      example = lib.literalExpression ''
        '''
        curl -f http://localhost:5000/users
        '''
      '';
    };

    testScript = lib.mkOption {
      internal = true;
      type = lib.types.str;
      default = ''
        machine.start()
        machine.wait_for_unit("multi-user.target")
        ${lib.concatMapAttrsStringSep "\n" (
          name: _: "machine.wait_for_unit(\"${name}.service\")"
        ) app.services.components}
        machine.succeed("${pkgs.writeShellScript "${app.name}-test-script" config.script}")
      '';
      description = "Python test script passed to the NixOS test driver.";
    };

    result = {
      build = lib.mkOption {
        internal = true;
        readOnly = true;
        type = lib.types.package;
        description = "NixOS test derivation.";
      };

      # HACK:
      # Prevent toJSON from attempting to convert the `build` option,
      # which won't work because it's a whole NixOS test evaluation.
      __toString = lib.mkOption {
        internal = true;
        readOnly = true;
        type = with lib.types; functionTo str;
        default = self: "nixos-test";
      };
    };
  };

  config = {
    result.build = pkgs.testers.runNixOSTest {
      name = "${app.name}-test";
      nodes.machine = {
        imports = app.services.runtimes.nixos.result.modules;
        system.stateVersion = "25.11";
        environment.systemPackages = app.programs.requirements ++ config.requirements;
      };
      inherit (config) testScript;
    };
  };
}
